buildscript {
  dependencies {
    classpath 'net.sf.proguard:proguard-gradle:6.0.3'
  }
}

plugins {
    id 'com.typelead.eta' version '0.7.2'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
}

eta {
    useSystemEta = true
    useSystemEtlas = true
}

dependencies {
  compile eta('base:[4,5[')
  compile eta('binary:[0.7,0.9[')
  compile eta('bytestring:0.10.+')
  compile eta('filepath')
  compile eta('directory')
  compile eta('deepseq:[1.4,1.5[')
  compile eta('eta-repl')
  compile eta('eta-meta')
}

compileEta {
  options.args = ['-Wall', '-Werror']
}

jar {
  manifest {
    attributes 'Main-Class': "eta.main"
  }
}

shadowJar {
  zip64 = true
}

import proguard.gradle.ProGuardTask

if (!JavaVersion.current().isJava9Compatible()) {

  task proJar(type: ProGuardTask) {
    configuration defaultEtaProguardFile
    configuration 'proguard.txt'

    injars shadowJar.outputs
    outjars 'build/eta-serv.jar'
  }

} else {

  task proJar(type: Copy) {
    from shadowJar.outputs
    into 'build'
    rename { 'eta-serv.jar' }
  }

}
